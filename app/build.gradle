apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    ndkVersion '22.0.7026061'

    defaultConfig {
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion 29
        versionCode 61
        versionName '1.61'

        buildConfigField 'String', 'SERVICE_TOKEN', getProps("vk_service_token")
        buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("youtube_dev_key")
        buildConfigField 'String', 'FAKE_ABI', getProps("fake_abi")
        buildConfigField 'String', 'FAKE_DEVICE', getProps("fake_device")
        buildConfigField 'String', 'FCM_SESSION_ID_GEN_URL', getProps("fcm_session_id_gen_url")
        buildConfigField 'String', 'KATE_RECEIPT_GMS_TOKEN', getProps("kate_receipt_gms_token")
        resValue "string", "kate_receipt_gms_token", getProps("kate_receipt_gms_token")

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }

    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=compatibility']
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    flavorDimensions "type"

    productFlavors {
        fenrir_vk_lite {
            applicationId "dev.ragnarok.fenrir_public"
            dimension "type"
            buildConfigField 'boolean', 'IS_DONATE', 'false'
            buildConfigField 'int', 'VK_API_APP_ID', getProps("vk_app_id")
            buildConfigField 'int', 'DEFAULT_ACCOUNT_TYPE', 'Account_Types.VK_ANDROID'
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("vk_client_secret")
        }
        fenrir_vk_full {
            applicationId "dev.ragnarok.fenrir_full"
            dimension "type"
            buildConfigField 'boolean', 'IS_DONATE', 'true'
            buildConfigField 'int', 'VK_API_APP_ID', getProps("vk_app_id")
            buildConfigField 'int', 'DEFAULT_ACCOUNT_TYPE', 'Account_Types.VK_ANDROID'
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("vk_client_secret")
        }
        fenrir_kate_lite {
            applicationId "dev.ragnarok.fenrir_kate_public"
            dimension "type"
            buildConfigField 'boolean', 'IS_DONATE', 'false'
            buildConfigField 'int', 'VK_API_APP_ID', getProps("kate_app_id")
            buildConfigField 'int', 'DEFAULT_ACCOUNT_TYPE', 'Account_Types.KATE'
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("kate_client_secret")
        }
        fenrir_kate_full {
            applicationId "dev.ragnarok.fenrir_kate_full"
            dimension "type"
            buildConfigField 'boolean', 'IS_DONATE', 'true'
            buildConfigField 'int', 'VK_API_APP_ID', getProps("kate_app_id")
            buildConfigField 'int', 'DEFAULT_ACCOUNT_TYPE', 'Account_Types.KATE'
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("kate_client_secret")
        }
    }
}

def getProps(String propName) {
    def propsFile = rootProject.file('app.build_config')

    if (propsFile.exists()) {
        def props = new Properties()

        FileInputStream fis
        try {
            fis = new FileInputStream(propsFile)
            props.load(fis)

            def value = props[propName]
            fis.close()
            return value
        } finally {
            if (fis != null) fis.close()
        }
    } else {
        throw new GradleException('File app.build_config not found')
    }
}

ext {
    exoLibraryVersion = '2.13.2'
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-parcelize-runtime:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-android-extensions-runtime:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.4.0-alpha01'
    implementation 'io.coil-kt:coil:1.1.1'
    implementation 'io.coil-kt:coil-gif:1.1.1'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.customview:customview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.10'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'com.google.firebase:firebase-database:19.6.0'
    fenrir_vk_liteImplementation('com.google.firebase:firebase-messaging:21.0.1') {
        exclude group: "com.google.firebase", module: "firebase-installations"
    }
    fenrir_vk_liteImplementation project(path: ':firebase-installations')
    fenrir_vk_fullImplementation('com.google.firebase:firebase-messaging:21.0.1') {
        exclude group: "com.google.firebase", module: "firebase-installations"
    }
    fenrir_vk_fullImplementation project(path: ':firebase-installations')
    //noinspection GradleDependency
    fenrir_kate_liteImplementation 'com.google.firebase:firebase-messaging:20.1.0'
    //noinspection GradleDependency
    fenrir_kate_fullImplementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation project(path: ':ealvatag')
    implementation project(path: ':picasso')
    implementation project(path: ':slidr')
    implementation project(path: ':ucrop')
    implementation project(path: ':image')
    implementation project(path: ':zxing-android-embedded')
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitLibraryVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibraryVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibraryVersion"
    implementation "com.squareup.okio:okio:2.10.0"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoLibraryVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoLibraryVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.media:media:1.2.1'
    implementation 'androidx.activity:activity-ktx:1.2.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.work:work-runtime-ktx:2.5.0'
    implementation 'com.google.guava:guava:30.1-android'
    implementation 'com.caverock:androidsvg:1.4'

    //hidden
    implementation 'com.google.android.datatransport:transport-runtime:2.2.6'
    implementation 'com.google.android.datatransport:transport-backend-cct:2.3.3'
    implementation 'com.google.android.datatransport:transport-api:2.2.1'
    implementation 'androidx.versionedparcelable:versionedparcelable:1.1.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.loader:loader:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'
}

apply plugin: 'com.google.gms.google-services'
